# Maintainer: Jerome Leclanche <jerome@leclan.ch>

_pkgname=liblxqt
pkgname=$_pkgname-git
pkgver=0.7.0.32.gbcfa097
pkgrel=1
pkgdesc="Common base library for LXQt components."
arch=("i686" "x86_64" "armv6h")
url="http://lxqt.org"
license=("GPL2")
depends=("qt5-base" "qt5-x11extras" "qt5-tools" "libqtxdg-qt5-git")
makedepends=("git" "cmake")
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git://git.lxde.org/git/lxde/$_pkgname.git")
sha256sums=("SKIP")

pkgver() {
	cd "$srcdir/$_pkgname"
	git describe --always | sed "s/-/./g"
}

build() {
	cd "$srcdir/$_pkgname"
	mkdir -p build
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DUSE_QT5=true ..
	make
}

package() {
	cd "$srcdir/$_pkgname"
	cd build
	make DESTDIR="$pkgdir" install
}

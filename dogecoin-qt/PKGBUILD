#Maintainer thermi
#I Used the packet "dogecoin-qt-git" of Gee as a base for this one
#Based of primecoin-qt by Daniel Spies

pkgname=dogecoin-qt
_binname=dogecoin
pkgver=1.6
pkgrel=2
pkgdesc="Cryptocurrency"
arch=('x86_64' 'i686')
url="http://dogecoin.com/"
license=('MIT')
provides=('dogecoin-qt')
depends=('qt4' 'miniupnpc' 'boost-libs')
makedepends=('boost' 'gcc' 'make' 'git')
source=('https://github.com/dogecoin/dogecoin/archive/1.6.tar.gz'
        'dogecoin.desktop')
install=dogecoin.install

sha256sums=('cab04d7a33de1b429440bfa98506d4a64417a70bddcb9a33342b38837edf54fd'
            '679d16a093c3f8d92d9ad0f0cd4af8888e2ceae7ba507caabd1ba0d5e56f5d76')

## files & commands for building
_qmake=qmake-qt4

build() {
	cd ${srcdir}/dogecoin-$pkgver/
	
	${_qmake} QMAKE_CFLAGS_RELEASE="$CPPFLAGS $CFLAGS" QMAKE_CXXFLAGS_RELEASE="$CPPFLAGS $CXXFLAGS" QMAKE_LFLAGS_RELEASE="$LDFLAGS" ${_binname}-qt.pro

	make ${MAKEFLAGS}
}

package() {
	install -Dm644 ${_binname}.desktop "${pkgdir}/usr/share/applications/${_binname}.desktop"

	cd "$srcdir"/dogecoin-$pkgver
	install -Dm755 ${_binname}-qt "${pkgdir}/usr/bin/${_binname}-qt"
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
	install -Dm644 src/qt/res/icons/bitcoin.png "${pkgdir}/usr/share/pixmaps/${_binname}.png"
}

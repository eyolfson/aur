# Maintainer: Doug Newgard <scimmia at archlinux dot info>
# Contributor: Sergio Correia <sergio@correia.cc>
# Contributor: Nicolas Vivet <nizzox@gmail.com>

_pkgname=libphutil
pkgname=$_pkgname-git
pkgver=5.r909.g610ca9e
pkgrel=1
pkgdesc='Library system which organizes PHP classes and functions into modules'
arch=('any')
url="http://phabricator.com"
license=('Apache')
depends=('php')
makedepends=('git')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
source=("git+https://secure.phabricator.com/diffusion/PHU/libphutil.git")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --tags --always | sed 's/^conduit-//;s/-/.r/;s/-/./'
}

prepare() {
# Don't override user option, doesn't work right anyway
  sed -i "s|^\s*'error_log'|//&|" $_pkgname/scripts/__init_script__.php
}

package() {
  install -d "$pkgdir/usr/share/php/$_pkgname" "$pkgdir/etc/php/conf.d/"
# do not copy hidden directories
  cp -a $_pkgname/* "$pkgdir/usr/share/php/$_pkgname/"
  echo 'open_basedir = "${open_basedir}:/usr/share/php/"' > "$pkgdir/etc/php/conf.d/libphutil.ini"
}

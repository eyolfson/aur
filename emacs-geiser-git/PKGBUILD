# Maintainer: Jon Eyolfson <jon@eyl.io>

pkgname=emacs-geiser-git
pkgver=0.7.r37
pkgrel=1
pkgdesc="A major mode for Emacs-Scheme interaction"
url="http://www.nongnu.org/geiser/"
arch=('any')
license=('GPL3')
depends=('emacs')
makedepends=('git')
provides=('emacs-geiser')
conflicts=('emacs-geiser')
source=("git://git.sv.gnu.org/geiser.git")
md5sums=('SKIP')

pkgver() {
  cd geiser
  git describe --tags --long | sed -r 's/^v//;s/([^-]*-g)/r\1/;s/-[^-]*$//;s/-/./g'
}

build() {
  cd geiser
  ./autogen.sh
  ./configure --prefix="/usr"
  make
}

package() {
  cd geiser
  make DESTDIR="${pkgdir}" install
}

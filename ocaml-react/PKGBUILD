# Maintainer: Serge Zirukin <ftrvxmtrx@gmail.com>
# Contributor: Sergei Lebedev <superbobry@gmail.com>
# Contributor: serp <serp 256 at gmail dot com>

pkgname=ocaml-react
pkgver=0.9.4
pkgrel=2
pkgdesc="An OCaml module for functional reactive programming"
arch=('i686' 'x86_64')
url="http://erratique.ch/software/react"
license=('BSD')
depends=('ocaml')
makedepends=('ocaml-findlib')
source=(http://erratique.ch/software/react/releases/react-$pkgver.tbz)
md5sums=('ea5edd6475ad305e084744571e0c9209')

build() {
  cd "$srcdir/react-$pkgver"
  ocaml setup.ml -configure --prefix /usr --destdir $pkgdir --disable-tests
  ocaml setup.ml -build
}

package() {
  cd "$srcdir/react-$pkgver"
  export OCAMLFIND_DESTDIR="$pkgdir/$(ocamlfind printconf destdir)"
  install -dm755 "$OCAMLFIND_DESTDIR"
  ocaml setup.ml -install
}

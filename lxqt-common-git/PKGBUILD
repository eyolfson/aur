# Maintainer: Jerome Leclanche <jerome@leclan.ch>

_pkgname=lxqt-common
pkgname=$_pkgname-git
pkgver=0.7.0.11.g20c18c0
pkgrel=1
pkgdesc="Common data to run a LXQt session."
arch=("i686" "x86_64" "armv6h")
url="http://lxqt.org"
license=("GPL2")
makedepends=("git" "cmake")
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git://git.lxde.org/git/lxde/$_pkgname.git")
sha256sums=("SKIP")

pkgver() {
	cd "$srcdir/$_pkgname"
	git describe --always | sed "s/-/./g"
}

build() {
	cd "$srcdir/$_pkgname"
	mkdir -p build
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DUSE_QT5=true ..
	make
}

package() {
	cd "$srcdir/$_pkgname"
	cd build
	make DESTDIR="$pkgdir" install
}

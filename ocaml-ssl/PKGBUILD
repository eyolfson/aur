# Contributor: Denis Wernert <denis@wernert.info>
pkgname=ocaml-ssl
pkgver=0.4.7
pkgrel=3
pkgdesc="OCaml SSL Library"
arch=('i686' 'x86_64')
url="http://savonet.sourceforge.net/"
license=('custom')
depends=('openssl')
makedepends=('ocaml' 'ocaml-findlib')
source=(ocaml-ssl-0.4.7.tar.gz::http://sourceforge.net/projects/savonet/files/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz/download)
options=(!libtool !strip zipman !makeflags staticlibs)
md5sums=('873533ce43cf4b88b4fb0ea27289320a')


build() {
  cd $pkgname-$pkgver
  ./configure --prefix /usr
  make
}

package() {
  OCAMLFIND_DESTDIR="${pkgdir}$(ocamlfind printconf destdir)"
  cd $pkgname-$pkgver
  mkdir -p $OCAMLFIND_DESTDIR/stublibs
  OCAMLFIND_INSTFLAGS="-destdir $OCAMLFIND_DESTDIR -ldconf /dev/null" make install

  mkdir -p $pkgdir/usr/share/licenses/$pkgname/
  cat README | cat README | awk 'BEGIN{P=0} /License/ {P = 1;} {if (P) print}' > $pkgdir/usr/share/licenses/$pkgname/license
}

# vim:set ts=2 sw=2 et:

# Maintainer: Michael Sproul <micsproul@gmail.com>
# Request Changes: https://github.com/michaelsproul/rust-nightly-archlinux
pkgname=rust-nightly-bin
pkgver=0.13.0_2014.11.11
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='A safe, concurrent, practical language from Mozilla.'
url='http://www.rust-lang.org/'
provides='rust'
conflicts=('rust' 'rust-git' 'rust-nightly')
depends=('shared-mime-info')
license=('MIT' 'Apache')
source=("https://static.rust-lang.org/dist/rust-nightly-${CARCH}-unknown-linux-gnu.tar.gz"
        'https://raw.githubusercontent.com/rust-lang/rust/master/src/etc/gedit/share/mime/packages/rust.xml')
sha256sums=('SKIP' 'SKIP')
install=rust.install
options=(staticlibs !strip)

package() {
    # Rust
    cd rust-nightly-${CARCH}-unknown-linux-gnu
    ./install.sh --prefix=$pkgdir/usr

    # Docs
    local DOC_DIR=$pkgdir/usr/share/doc/rust/html
    echo "Copying HTML docs to ${DOC_DIR}"
    mkdir -p ${DOC_DIR}
    cp -r doc/* ${DOC_DIR}

    echo "Setting permissions on docs."
    find ${DOC_DIR} -type f -exec chmod 644 {} ';'
    find ${DOC_DIR} -type d -exec chmod 775 {} ';'

    # MIME type association
    mkdir -p "${pkgdir}/usr/share/mime/packages"
    install -m 644 "${srcdir}/rust.xml" "${pkgdir}/usr/share/mime/packages/rust.xml"
}

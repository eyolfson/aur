pkgname=sabnzbd
_pkgname=SABnzbd
pkgver=2.0.1
pkgrel=1
pkgdesc="A web-interface based binary newsgrabber with NZB file support"
url="http://www.sabnzbd.org"
arch=('any')
license=('GPL')
depends=('curl' 'par2cmdline' 'python2' 'python2-cheetah' 'python2-yenc'
         'sqlite' 'unrar' 'unzip')
optdepends=('python2-feedparser: rss support'
            'python2-pyopenssl: ssl support')
source=("https://github.com/${pkgname}/${pkgname}/releases/download/${pkgver}/${_pkgname}-${pkgver}-src.tar.gz"
        "${pkgname}@.service")
sha256sums=('62619aab1c741e5d9088bfa177418da2961b82e58f93a763154514fb9d94880f'
            '5a2ca03b639d64250113a5e04e3884e2628cc4f3c23b42a39d15e69ec3261507')

package() {
  install -d "${pkgdir}/opt/${pkgname}"
  cp -a "${srcdir}/${_pkgname}-${pkgver}/"* "${pkgdir}/opt/${pkgname}"

  # Fix for issues with Python 3
  find "${pkgdir}/opt/${pkgname}" -type f -exec sed -i 's/python/python2/g' {} \;
  find "${pkgdir}/opt/${pkgname}" -type d -exec chmod 755 {} \;
  find "${pkgdir}/opt/${pkgname}" -type f -exec chmod 644 {} \;
  chmod 755 "${pkgdir}/opt/${pkgname}/${_pkgname}.py"

  install -Dm644 "${srcdir}/${pkgname}@.service" "${pkgdir}/usr/lib/systemd/system/${pkgname}@.service"
}

# Maintainer: Jon Eyolfson <jon@eyolfson.com>

pkgname=prezto
pkgver=r1509
pkgrel=1
pkgdesc="Prezto is a configuration framework for Zsh"
arch=('any')
url="https://github.com/sorin-ionescu/prezto"
license=('MIT')
depends=('zsh>=4.3.17')
makedepends=('git')
options=('!strip')
source=("git://github.com/sorin-ionescu/prezto.git"
        "git://github.com/zsh-users/zsh-history-substring-search.git"
        "git://github.com/zsh-users/zsh-syntax-highlighting.git"
        "git://github.com/zsh-users/zsh-completions.git"
        "git://github.com/davidjrice/prezto_powerline.git"
        "git://github.com/agnoster/agnoster-zsh-theme.git"
        "git://github.com/sindresorhus/pure.git"
        "git://github.com/tarruda/zsh-autosuggestions.git")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

pkgver() {
	cd "$pkgname"
	printf "r%s" "$(git rev-list --count HEAD)"
}

prepare() {
  cd "$pkgname"
  git submodule init
  git config submodule.modules/history-substring-search/external.url $srcdir/zsh-history-substring-search
  git config submodule.modules/syntax-highlighting/external.url $srcdir/zsh-syntax-highlighting
  git config submodule.modules/completion/external.url $srcdir/zsh-completions
  git config submodule.modules/prompt/external/powerline.url $srcdir/prezto_powerline
  git config submodule.modules/prompt/external/agnoster.url $srcdir/agnoster-zsh-theme
  git config submodule.modules/prompt/functions/pure.url $srcdir/pure
  git config submodule.modules/autosuggestions/external.url $srcdir/zsh-autosuggestions
  git submodule update

  sed -i 's#\${ZDOTDIR:-\$HOME}/\.zprezto/#/opt/prezto/#g' init.zsh
}

package() {
  cd "$pkgname"
  mkdir -p "$pkgdir/opt/prezto"
  cp -r "$srcdir/$pkgname/"* "$pkgdir/opt/prezto"
}

# Maintainer: Dylan <dylan@hashflo.net>

pkg_base=dogecoin
pkgname=('dogecoin-daemon')
pkgver=1.8
pkgrel=2
arch=('i686' 'x86_64')
url="http://dogecoin.com/"
makedepends=('boost' 'automoc4' 'miniupnpc' 'protobuf')
license=('MIT')
source=(https://github.com/dogecoin/dogecoin/archive/$pkgver-maint.tar.gz
	https://raw.githubusercontent.com/dogecoin/dogecoin/$pkgver-maint/contrib/debian/examples/bitcoin.conf
	https://raw.github.com/dogecoin/dogecoin/$pkgver-maint/contrib/debian/manpages/bitcoind.1
	https://raw.github.com/dogecoin/dogecoin/$pkgver-maint/contrib/debian/manpages/bitcoin.conf.5)
sha256sums=('021643f5410894e7286d71bc12a46ab4dedd116f91ca5d33fc48cdde9f8204fc'
            'ad93d42db719310b71129ada2e69c5dce438cbaa900c90c2d8ee062a014cbb88'
            '33deda1a0d805a10cae0f16100f32367d4d109b9deac55a67ecd7e2ec49f80e6'
            '20ce26d2953f34a541f8a0b08b7e6093aa729d11044a60bf0ff4574f97dd36af')
            
build() {
  cd "$srcdir/$pkg_base-$pkgver-maint"
  ./autogen.sh
  ./configure --prefix=/usr --with-incompatible-bdb
  make
}

package_dogecoin-daemon() {
  pkgdesc="Dogecoin is a peer-to-peer network based digital currency - daemon"
  depends=(boost-libs miniupnpc openssl)
  cd "$srcdir/$pkg_base-$pkgver-maint"
  install -Dm755 src/dogecoind "$pkgdir"/usr/bin/dogecoind
  install -Dm644 "$srcdir"/bitcoin.conf \
    "$pkgdir/usr/share/doc/$pkgname/examples/dogecoin.conf"
  install -Dm644 "$srcdir"/bitcoind.1 \
    "$pkgdir"/usr/share/man/man1/dogecoind.1
  install -Dm644 "$srcdir"/bitcoin.conf.5 \
    "$pkgdir"/usr/share/man/man5/dogecoin.conf.5
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}


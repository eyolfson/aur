# Maintainer: Dylan <dylan@hashflo.net>

pkg_base=dogecoin
pkgname=('dogecoin-daemon')
pkgver=1.7.1
pkgrel=1
arch=('i686' 'x86_64')
url="http://dogecoin.com/"
makedepends=('boost' 'automoc4' 'miniupnpc' 'protobuf')
license=('MIT')
source=(https://github.com/dogecoin/dogecoin/archive/v$pkgver.tar.gz
	https://raw.githubusercontent.com/dogecoin/dogecoin/v$pkgver/contrib/debian/examples/bitcoin.conf
	https://raw.github.com/dogecoin/dogecoin/v$pkgver/contrib/debian/manpages/bitcoind.1
	https://raw.github.com/dogecoin/dogecoin/v$pkgver/contrib/debian/manpages/bitcoin.conf.5)
sha256sums=('dc47617f7504621a43d1a795ce5945a04c09d8bc721f16800b2d15538f88a2e7'
            'ad93d42db719310b71129ada2e69c5dce438cbaa900c90c2d8ee062a014cbb88'
            '33deda1a0d805a10cae0f16100f32367d4d109b9deac55a67ecd7e2ec49f80e6'
            '5c7ba91f2f91a990616483a4a8243faf259cf12e82023dbe9176836394bd9cb0')
            
build() {
  cd "$srcdir/$pkg_base-$pkgver"
  ./autogen.sh
  ./configure --prefix=/usr --with-incompatible-bdb
  make
}

package_dogecoin-daemon() {
  pkgdesc="Dogecoin is a peer-to-peer network based digital currency - daemon"
  depends=(boost-libs miniupnpc openssl)
  cd "$srcdir/$pkg_base-$pkgver"
  install -Dm755 src/dogecoind "$pkgdir"/usr/bin/dogecoind
  install -Dm644 "$srcdir"/bitcoin.conf \
    "$pkgdir/usr/share/doc/$pkgname/examples/dogecoin.conf"
  install -Dm644 "$srcdir"/bitcoind.1 \
    "$pkgdir"/usr/share/man/man1/dogecoind.1
  install -Dm644 "$srcdir"/bitcoin.conf.5 \
    "$pkgdir"/usr/share/man/man5/dogecoin.conf.5
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

